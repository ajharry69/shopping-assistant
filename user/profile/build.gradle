apply plugin: "com.android.dynamic-feature"
apply plugin: 'kotlin-android'

android {
    compileSdkVersion sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = jvm_target_version
            freeCompilerArgs += [
                    "-Xuse-experimental=kotlin.Experimental",
                    '-Xopt-in=kotlin.time.ExperimentalTime',
                    '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                    '-Xopt-in=kotlinx.coroutines.FlowPreview',
            ]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        def srcDir = 'src/sharedTest/java'
        test {
            java.srcDirs += srcDir
        }
        androidTest {
            java.srcDirs += srcDir
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true // required to not break unit tests
        }
    }

    packagingOptions {
        pickFirst 'win32-x86-64/attach_hotspot_windows.dll'
        pickFirst 'win32-x86/attach_hotspot_windows.dll'
        pickFirst 'META-INF/licenses/ASM'
        pickFirst 'META-INF/AL2.0'
        pickFirst 'META-INF/LGPL2.1'
    }

}

dependencies {
    implementation project(":app")

    // Local unit tests.
    testImplementation project(path: ':tests:unit')

    // Instrumented tests.
    androidTestImplementation project(path: ':tests:ui')
    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.
    debugImplementation("androidx.fragment:fragment-testing:$fragment_version") {
        exclude group: 'androidx.test', module: 'core'
    }
}